plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
    id "de.mannodermaus.android-junit5"
}

android {
    namespace 'io.github.afalalabarce.mvvmproject'
    compileSdk 33

    defaultConfig {
        applicationId "io.github.afalalabarce.mvvmproject"
        minSdk 23
        targetSdk 33
        multiDexEnabled true
        versionCode 100
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments["runnerBuilder"] = "de.mannodermaus.junit5.AndroidJUnit5Builder"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    /* TODO: Pending set keystore signature data
    signingConfigs {
        release {
            storeFile file("keystore/MVVMProject.jks")
            storePassword "mvvmprojectpassword"
            keyAlias "MVVMProjectSignature"
            keyPassword "mvvmprojectpassword"
        }
    }
    */

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // Force copy of distributable apk to custom folder dist in root project
            def archiveBuildTypes = [ "release", "debug" ]
            def distFolder = "../dist/"
            def apkName = android.defaultConfig.applicationId

            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    if (variant.buildType.name in archiveBuildTypes) {
                        // Update output filename
                        if (variant.versionName != null) {
                            String name = "$apkName-${variant.versionName}-${output.baseName}.apk"
                            outputFileName = new File(name)
                        }
                        def taskSuffix = variant.name.capitalize()
                        def assembleTaskName = "assemble${taskSuffix}"
                        if (tasks.findByName(assembleTaskName)) {
                            def copyAPKFolderTask = tasks.create(name: "archive${taskSuffix}", type: org.gradle.api.tasks.Copy) {
                                description "Archive/copy APK folder to a shared folder."
                                def sourceFolder = "$buildDir/outputs/apk/${output.baseName.replace("-", "/")}"
                                def destinationFolder = "$distFolder${output.baseName.replace("-", "/")}"
                                print "Copying APK folder from: $sourceFolder into $destinationFolder\n"
                                from(sourceFolder)
                                into destinationFolder
                                eachFile { file ->
                                    file.path = file.name // so we have a "flat" copy
                                }
                                includeEmptyDirs = false
                            }
                            tasks[assembleTaskName].finalizedBy = [copyAPKFolderTask]
                        }
                    }
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
        coreLibraryDesugaringEnabled true

    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    buildFeatures {
        compose true
    }

    viewBinding {
        enabled = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion "$compose_compiler_version"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    // Needed for testing usage
    testOptions {
        animationsDisabled=true
        reportDir "$rootDir/instrumentedTestsResults/reports/$project.name"
        resultsDir "$rootDir/instrumentedTestsResults/results/$project.name"
        unitTests{
            includeAndroidResources = true
            returnDefaultValues = true
            all {
                useJUnitPlatform()
            }
        }
    }
}

// This tasks are for future automation console scripts
tasks.register('applicationId'){
    doLast{
        println "${android.defaultConfig.applicationId}"
    }
}

tasks.register('appVersionName'){
    doLast{
        println "${android.defaultConfig.versionName}"
    }
}


dependencies {
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation project(path: ':model')
    implementation project(path: ':domain')
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugar_version"
    implementation "com.google.code.gson:gson:$gson_version"
    implementation "androidx.multidex:multidex:$multidex_version"

    // Koin for Android
    implementation "io.insert-koin:koin-android:$koin_version"
    implementation "io.insert-koin:koin-annotations:$koin_ksp_version"
    ksp "io.insert-koin:koin-ksp-compiler:$koin_ksp_version"

    // Kotlin Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"

    // Lifecycle dependencies
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$livedata_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_ext_version"
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"

    implementation "androidx.activity:activity:$compose_activity_version"
    implementation "androidx.activity:activity-compose:$compose_activity_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.material3:material3:$material3_version"
    //implementation "com.google.android.material:material:$material_version"
    // this dependency provides extended collection of svg icons
    implementation "androidx.compose.material:material-icons-extended:$compose_version"

    // Navigation for compose (navcontroller and NavHost)
    implementation "androidx.navigation:navigation-compose:$nav_version"

    implementation "androidx.compose.animation:animation-core:$animation_compose_version"
    implementation "androidx.compose.animation:animation-graphics:$animation_compose_version"
    implementation "androidx.compose.animation:animation:$animation_compose_version"

    implementation "androidx.core:core-splashscreen:$android_splashscreen_version"

    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"
    implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"
    implementation "androidx.biometric:biometric-ktx:$androidx_biometric_version"

    // coil, like glide provides pictures asynchronously, but coil has better integration with compose
    implementation "io.coil-kt:coil-compose:$coil_version"
    implementation "io.coil-kt:coil-svg:$coil_version"
    implementation "io.coil-kt:coil-gif:$coil_version"
    implementation "io.coil-kt:coil-compose:$coil_version"

    // Testing implementation
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
    testImplementation "org.junit.jupiter:junit-jupiter:$jUnitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$jUnitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$jUnitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jUnitVersion"

    testImplementation "org.robolectric:robolectric:$roboelectric_version"

    testImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    testImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"
    testImplementation "com.agoda.kakao:kakao:$kakao_version"
    testImplementation "io.insert-koin:koin-test:$koin_version"
    testImplementation "io.insert-koin:koin-test-junit5:$koin_version"

    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "org.amshove.kluent:kluent-android:$kluent_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_test_android"
    testImplementation "androidx.arch.core:core-testing:$arch_core_test"
    testImplementation "androidx.test:core:1.5.0"
    testImplementation "androidx.test:core-ktx:1.5.0"

    androidTestImplementation "androidx.test.ext:junit:$androidxJtest"
    androidTestImplementation "androidx.test:runner:$androidx_test_core_version"
    androidTestImplementation "org.junit.jupiter:junit-jupiter:$jUnitVersion"
    androidTestImplementation "org.junit.jupiter:junit-jupiter-api:$jUnitVersion"
    androidTestImplementation "de.mannodermaus.junit5:android-test-core:$mannodermaus_junit5_version"
    androidTestRuntimeOnly "de.mannodermaus.junit5:android-test-runner:$mannodermaus_junit5_version"

    androidTestImplementation "io.insert-koin:koin-test:$koin_version"
    androidTestImplementation "io.insert-koin:koin-test-junit4:$koin_version"
    androidTestImplementation "io.mockk:mockk:$mockk_version"
    androidTestImplementation "org.amshove.kluent:kluent-android:$kluent_version"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_test_android"
    androidTestImplementation "androidx.arch.core:core-testing:$arch_core_test"
    androidTestImplementation "org.robolectric:robolectric:$roboelectric_version"


    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
}